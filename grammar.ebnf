items = { item };

item
= text
| element
;

text = char, { char };

char
= normal char
| escaped char
;

normal char = ? any unicode character ? - '\' - '<' - '>';

escaped char
= "\\"
| "\<"
| "\>"
;

element = '<', tag, ' ', items, '>';

tag = style, { ',', style };

style
= [ [ "fg" | "bg" ], ':' ], color
| [ [ "mod" ], ':' ], modifier
;

color
= "black"
| "white"
| light variant colors, [ '-' ]
| dark variant colors, [ '+' ]
| custom color
;

light variant colors
= "red"
| "green"
| "yellow"
| "blue"
| "magenta"
| "cyan"
;

dark variant colors = "gray";

custom color = 6 * hex_digit;

hex_digit
= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
| 'a' | 'b' | 'c' | 'd' | 'e' | 'f'
| 'A' | 'B' | 'C' | 'D' | 'E' | 'F'
;

modifier
= 'b' (* bold *)
| 'd' (* dim *)
| 'i' (* italic *)
| 'u' (* underline *)
| 's' (* slow blink *)
| 'r' (* rapid blink *)
| 'h' (* hide *)
| 'x' (* cross/delete line *)
;
